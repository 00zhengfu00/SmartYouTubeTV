buildscript {
    repositories {
        maven { url 'https://maven.fabric.io/public' }
    }

    dependencies {
        // These docs use an open ended version so that our plugin
        // can be updated quickly in response to Android tooling updates

        // We recommend changing it to the latest version from our changelog:
        // https://docs.fabric.io/android/changelog.html#fabric-gradle-plugin
        classpath 'io.fabric.tools:gradle:1.+'
    }
}

apply plugin: 'com.android.application'
// Put Fabric plugin after Android plugin
apply plugin: 'io.fabric'

repositories {
    maven { url 'https://maven.fabric.io/public' }
}

android {
    compileSdkVersion 25
    buildToolsVersion "25.0.0"

    /**
     * Original package name: com.liskovsoft.smartyoutubetv
     * GPlay package name: com.firsthash.smartyoutubetv2
     * 4PDA package name: com.firsthash.smartyoutubetv
     */

    defaultConfig {
        applicationId "com.firsthash.smartyoutubetv2"
        minSdkVersion 14
        targetSdkVersion 25
        versionCode 122
        versionName "6.8.51"
        multiDexEnabled = true

        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    // dimensions: http://tools.android.com/tech-docs/new-build-system/user-guide#TOC-Multi-flavor-variants
    flavorDimensions "webview", "xwalk", "exoplayer", "releaseType"

    productFlavors {
        webview {
            dimension "webview"
        }
        xwalk {
            dimension "xwalk"
        }
        exo {
            dimension "exoplayer"
        }
        orig {
            dimension "releaseType"
        }
        tmp {
            dimension "releaseType"
            applicationIdSuffix ".tmp"

            // custom resources location: src/betaFlavor
            // resValue "string", "app_name", "Smart YouTube TV Beta"
        }
        // change the root folders for Android productFlavors
        all {flavor ->
            sourceSets[flavor.name].setRoot("src/flavors/${flavor.name}")
        }
    }

    // naming example: SmartYouTubeTV_Xwalk_v6.8.12_r.apk
    // https://stackoverflow.com/questions/18332474/how-to-set-versionname-in-apk-filename-using-gradle
    applicationVariants.all { variant ->
        variant.outputs.each { output ->
            def project = "SmartYouTubeTV"
            def flavor = variant.productFlavors[-1].name.capitalize()
            def buildType = variant.variantData.variantConfiguration.buildType.name.take(1)
            def version = variant.versionName

            def newApkName = sprintf("%s_%s_v%s_%s.apk", [project, flavor, version, buildType])

            output.outputFile = new File(output.outputFile.parent, newApkName)
        }
    }

    lintOptions {
        disable 'MissingTranslation'
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    androidTestCompile('com.android.support.test.espresso:espresso-core:2.2.2', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })



    compile('com.crashlytics.sdk.android:crashlytics:2.6.5@aar') {
        transitive = true;
    }

    // uncomment if you want to use embeddable xwalk engine
    // originalFlavorCompile project(path: ':browser', configuration: "liteFlavorDebug")
    // betaFlavorCompile project(path: ':browser', configuration: "fullFlavorDebug")

    debugCompile project(path: ':browser', configuration: "liteDebug")
    releaseCompile project(path: ':browser', configuration: "liteRelease")

    compile 'com.android.support:appcompat-v7:25.4.0'
    compile 'com.jakewharton:butterknife:8.4.0'
    compile 'com.noveogroup.android:android-logger:1.3.5'
    compile 'com.google.android.exoplayer:exoplayer:r2.5.3'
    compile 'com.android.support.constraint:constraint-layout:1.0.0-beta4'
    testCompile 'junit:junit:4.12'
    testCompile 'commons-io:commons-io:2.5'
    testCompile 'org.robolectric:robolectric:3.2.2'
    annotationProcessor 'com.jakewharton:butterknife-compiler:8.4.0'
}
