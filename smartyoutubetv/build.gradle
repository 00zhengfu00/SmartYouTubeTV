buildscript {
    repositories {
        maven { url 'https://maven.fabric.io/public' }
        jcenter()
        google()
    }

    dependencies {
        // These docs use an open ended version so that our plugin
        // can be updated quickly in response to Android tooling updates

        // We recommend changing it to the latest version from our changelog:
        // https://docs.fabric.io/android/changelog.html#fabric-gradle-plugin
        classpath 'io.fabric.tools:gradle:1.+'

        classpath project.properties.resGuardVersion
    }
}

apply plugin: 'com.android.application'

// Put Fabric plugin after Android plugin
apply plugin: 'io.fabric'

apply plugin: 'AndResGuard'

repositories {
    maven { url 'https://maven.fabric.io/public' }
}

android {
    // FIX: Default interface methods are only supported starting with Android N (--min-api 24)
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    compileSdkVersion project.properties.compileSdkVersion
    buildToolsVersion project.properties.buildToolsVersion

    defaultConfig {
        minSdkVersion project.properties.minSdkVersion
        targetSdkVersion project.properties.targetSdkVersion
        applicationId "com.liskovsoft.smartyoutubetv"
        versionCode 508
        versionName "6.16.28"
        multiDexEnabled = true
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }

    signingConfigs {
        release {
            storeFile file("../android.keystore")
            storePassword project.KEYSTORE_PASSWORD
            keyAlias 'private'
            keyPassword project.KEYSTORE_PASSWORD
        }
    }

    buildTypes {
        // NOTE: to enable Crashlytics add fabric.properties file with your api keys
        Boolean crashEnabled = file('fabric.properties').exists()

        debug {
            buildConfigField "boolean", "CRASHLYTICS_ENABLED", crashEnabled.toString()
            ext.enableCrashlytics = crashEnabled
        }
        release {
            buildConfigField "boolean", "CRASHLYTICS_ENABLED", crashEnabled.toString()
            ext.enableCrashlytics = crashEnabled

            debuggable false
            minifyEnabled false
            shrinkResources false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'

            signingConfig signingConfigs.release
        }
    }

    // gradle 4.6 migration: disable dimensions mechanism
    // more: https://proandroiddev.com/advanced-android-flavors-part-4-a-new-version-fc2ad80c01bb
    flavorDimensions "default"

    productFlavors {
        orig {
            matchingFallbacks = ['lite']
        }
        V720 {
            matchingFallbacks = ['lite']
            versionCode 999 // don't auto-update
        }
        V1080 {
            matchingFallbacks = ['lite']
            versionCode 999 // don't auto-update
        }
        Vkids {
            matchingFallbacks = ['lite']
            applicationIdSuffix ".kids"
        }
        Vlive {
            matchingFallbacks = ['lite']
            applicationIdSuffix ".live"
        }
    }

    // naming example: SmartYouTubeTV_Xwalk_v6.8.12_r.apk
    // https://stackoverflow.com/questions/18332474/how-to-set-versionname-in-apk-filename-using-gradle
    applicationVariants.all { variant ->
        variant.outputs.each { output ->
            def project = "SYTV"
            def flavor = variant.productFlavors[-1].name.capitalize()
            def buildType = variant.variantData.variantConfiguration.buildType.name.take(1)
            def version = variant.versionName

            def newApkName = sprintf("%s_%s_v%s_%s.apk", [project, flavor, version, buildType])

            // old gradle 3.3:
            // output.outputFile = new File(output.outputFile.parent, newApkName)
            // gradle 4.6 migration
            output.outputFileName = new File(newApkName)
        }
    }

    lintOptions {
        abortOnError true
        lintConfig rootProject.file('lint.xml')
    }
}

andResGuard {
    // mappingFile = file("./resource_mapping.txt")
    mappingFile = null
    use7zip = true
    useSign = true
    // it will keep the origin path of your resources when it's true
    keepRoot = false

    whiteList = [
            // your icon
            "R.drawable.icon",
            // for fabric
            "R.string.com.crashlytics.*",
            // for google-services
            "R.string.google_app_id",
            "R.string.gcm_defaultSenderId",
            "R.string.default_web_client_id",
            "R.string.ga_trackingId",
            "R.string.firebase_database_url",
            "R.string.google_api_key",
            "R.string.google_crash_reporting_api_key"
    ]
    compressFilePattern = [
            "*.png",
            "*.jpg",
            "*.jpeg",
            "*.gif",
    ]
    sevenzip {
        artifact = 'com.tencent.mm:SevenZip:1.2.15'
        //path = "/usr/local/bin/7za"
    }

    /**
     * Optional: if finalApkBackupPath is null, AndResGuard will overwrite final apk
     * to the path which assemble[Task] write to
     **/
    // finalApkBackupPath = "${project.rootDir}/final.apk"

    /**
     * Optional: Specifies the name of the message digest algorithm to user when digesting the entries of JAR file
     * Only works in V1signing, default value is "SHA-1"
     **/
    // digestalg = "SHA-256"
}

dependencies {
    androidTestImplementation(project.properties.espressoCoreVersion, {
        exclude group: 'com.android.support', module: 'support-annotations'
    })
    testImplementation project.properties.junitVersion
    testImplementation project.properties.robolectricVersion
    testImplementation project.properties.commonsIOVersion

    implementation fileTree(dir: 'libs', include: ['*.jar'])

    // Common routines
    implementation project(path: ':common')

    // gradle 4.6 migration
    implementation project(path: ':browser')

    // gradle 4.6 migration
    implementation project(path: ':exoplayeractivity')

    // gradle 4.6 migration
    implementation project(path: ':appupdatechecker')

    implementation(project.properties.crashlyticsVersion) { // see top part of this file
        transitive = true
    }

    implementation project.properties.appCompatVersion
    implementation project.properties.constraintLayoutVersion

    implementation project.properties.butterKnifeVersion
    annotationProcessor project.properties.butterKnifeCompilerVersion

    implementation project.properties.gsonVersion
    implementation project.properties.jsonPathVersion

    // view dependencies: gradlew -q smartyoutubetv:dependencies
    // Details: https://docs.gradle.org/current/dsl/org.gradle.api.artifacts.ResolutionStrategy.html
    configurations.all {
        resolutionStrategy {
            force project.properties.asmVersion // resolve 'jsonPath' conflicts
        }
    }

    implementation project.properties.nanoHttpdVersion
    implementation project.properties.voiceOverlayVersion

    //implementation project.properties.exoplayerVersion
    implementation project.properties.amazonExoplayerVersion

    implementation project.properties.okhttpVersion
    implementation project.properties.ottoVersion
}
