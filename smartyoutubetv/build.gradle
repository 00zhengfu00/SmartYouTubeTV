buildscript {
    repositories {
        maven { url 'https://maven.fabric.io/public' }
    }

    dependencies {
        // These docs use an open ended version so that our plugin
        // can be updated quickly in response to Android tooling updates

        // We recommend changing it to the latest version from our changelog:
        // https://docs.fabric.io/android/changelog.html#fabric-gradle-plugin
        classpath 'io.fabric.tools:gradle:1.+'
    }
}

apply plugin: 'com.android.application'

// Put Fabric plugin after Android plugin
apply plugin: 'io.fabric'

repositories {
    maven { url 'https://maven.fabric.io/public' }
}

android {
    compileSdkVersion project.properties.compileSdkVersion
    buildToolsVersion project.properties.buildToolsVersion

    defaultConfig {
        minSdkVersion project.properties.minSdkVersion
        targetSdkVersion project.properties.targetSdkVersion
        applicationId "com.firsthash.smartyoutubetv2"
        versionCode 411
        versionName "6.15.51"
        multiDexEnabled = true

        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        // NOTE: to enable Crashlytics add fabric.properties file with your api keys
        Boolean crashEnabled = file('fabric.properties').exists()

        debug {
            buildConfigField "boolean", "CRASHLYTICS_ENABLED", crashEnabled.toString()
            ext.enableCrashlytics = crashEnabled
        }
        release {
            buildConfigField "boolean", "CRASHLYTICS_ENABLED", crashEnabled.toString()
            ext.enableCrashlytics = crashEnabled

            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    // gradle 4.6 migration: disable dimensions mechanism
    // more: https://proandroiddev.com/advanced-android-flavors-part-4-a-new-version-fc2ad80c01bb
    flavorDimensions "default"

    productFlavors {
        orig {
            matchingFallbacks = ['lite']
        }
        V720 {
            matchingFallbacks = ['lite']
            versionCode 999 // don't auto-update
        }
        V1080 {
            matchingFallbacks = ['lite']
            versionCode 999 // don't auto-update
        }
        Vkids {
            matchingFallbacks = ['lite']
            applicationIdSuffix ".kids"
        }
        Vlive {
            matchingFallbacks = ['lite']
            applicationIdSuffix ".live"
        }
    }

    // naming example: SmartYouTubeTV_Xwalk_v6.8.12_r.apk
    // https://stackoverflow.com/questions/18332474/how-to-set-versionname-in-apk-filename-using-gradle
    applicationVariants.all { variant ->
        variant.outputs.each { output ->
            def project = "SYTV"
            def flavor = variant.productFlavors[-1].name.capitalize()
            def buildType = variant.variantData.variantConfiguration.buildType.name.take(1)
            def version = variant.versionName

            def newApkName = sprintf("%s_%s_v%s_%s.apk", [project, flavor, version, buildType])

            // old gradle 3.3:
            // output.outputFile = new File(output.outputFile.parent, newApkName)
            // gradle 4.6 migration
            output.outputFileName = new File(newApkName)
        }
    }

    lintOptions {
        abortOnError true
        disable 'MissingTranslation'
        disable 'NewApi'
    }
}

dependencies {
    androidTestApi(project.properties.espressoCoreVersion, {
        exclude group: 'com.android.support', module: 'support-annotations'
    })
    testApi project.properties.junitVersion
    testApi project.properties.robolectricVersion
    testApi project.properties.commonsIOVersion

    api fileTree(dir: 'libs', include: ['*.jar'])

    // Common routines
    implementation project(path: ':common')

    // gradle 4.6 migration
    implementation project(path: ':browser')

    // gradle 4.6 migration
    implementation project(path: ':exoplayeractivity')

    // gradle 4.6 migration
    implementation project(path: ':appupdatechecker')

    api(project.properties.crashlyticsVersion) { // see top part of this file
        transitive = true
    }

    api project.properties.appCompatVersion
    api project.properties.constraintLayoutVersion

    api project.properties.androidLoggerVersion

    api project.properties.butterKnifeVersion
    annotationProcessor project.properties.butterKnifeCompilerVersion

    api project.properties.gsonVersion
    api project.properties.jsonPathVersion

    // view dependencies: gradlew -q smartyoutubetv:dependencies
    // Details: https://docs.gradle.org/current/dsl/org.gradle.api.artifacts.ResolutionStrategy.html
    configurations.all {
        resolutionStrategy {
            force project.properties.asmVersion // resolve 'jsonPath' conflicts
        }
    }

    api project.properties.nanoHttpdVersion
    api project.properties.voiceOverlayVersion
}
